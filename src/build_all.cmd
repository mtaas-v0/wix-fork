@setlocal
@pushd %~dp0

@set _C=Debug
:parse_args
@if /i "%1"=="release" set _C=Release
@if not "%1"=="" shift & goto parse_args

@if "%VCToolsVersion%"=="" call :StartDeveloperCommandPrompt || exit /b

@echo build %_C%

:: Initialize required files/folders

call build_init.cmd

:: DTF

echo "::group::dtf.cmd"
call dtf\dtf.cmd %_C% || exit /b
echo "::endgroup::"

:: internal
echo "::group::internal.cmd"
call internal\internal.cmd %_C% || exit /b
echo "::endgroup::"


:: libs
echo "::group::libs.cmd"
call libs\libs.cmd %_C% || exit /b
echo "::endgroup::"


:: api
echo "::group::api.cmd"
call api\api.cmd %_C% || exit /b
echo "::endgroup::"


:: burn
echo "::group::burn.cmd"
call burn\burn.cmd %_C% || exit /b
echo "::endgroup::"


:: wix
echo "::group::wix.cmd"
call wix\wix.cmd %_C% || exit /b
echo "::endgroup::"


:: tools
echo "::group::tools.cmd"
call tools\tools.cmd %_C% || exit /b
echo "::endgroup::"


:: ext
echo "::group::ext.cmd"
call ext\ext.cmd %_C% || exit /b
echo "::endgroup::"


:: setup
echo "::group::setup.cmd"
call setup\setup.cmd %_C% || exit /b
echo "::endgroup::"


:: integration tests
echo "::group::test.cmd"
call test\test.cmd %_C% || exit /b
echo "::endgroup::"


:: finalize build
echo "::group::finalize.cmd"
call internal\finalize.cmd %_C% || exit /b
echo "::endgroup::"


goto LExit

:StartDeveloperCommandPrompt
if not "%WixSkipVsDevCmd%"=="" (
  echo Skipping initializing developer command prompt
  exit /b
)

echo Initializing developer command prompt

if not exist "%ProgramFiles(x86)%\Microsoft Visual Studio\Installer\vswhere.exe" (
  "%ProgramFiles(x86)%\Microsoft Visual Studio\Installer\vswhere.exe"
  exit /b 2
)

for /f "usebackq delims=" %%i in (`"%ProgramFiles(x86)%\Microsoft Visual Studio\Installer\vswhere.exe" -version [17^,19^) -property installationPath`) do (
  if exist "%%i\Common7\Tools\vsdevcmd.bat" (
    call "%%i\Common7\Tools\vsdevcmd.bat" -no_logo
    exit /b
  )
  echo developer command prompt not found in %%i
)

echo No versions of developer command prompt found
exit /b 2

:LExit
@popd
@endlocal
